varnishtest "Test digest vmod"

# Test vectors borrowed from wikipedia (which is in turn borrowed from
# rfc2104)

server s1 {
       rxreq
       txresp
} -start

varnish v1 -vcl+backend {
	import digest from "${vmod_topbuild}/src/.libs/libvmod_digest.so";

	sub vcl_deliver {
		set resp.http.sha256 = digest.hmac_sha256("key", "The quick brown fox jumps over the lazy dog");
		set resp.http.sha1 = digest.hmac_sha1("key", "The quick brown fox jumps over the lazy dog");
		set resp.http.md5 = digest.hmac_md5("key", "The quick brown fox jumps over the lazy dog");
		set resp.http.sha256_b64 = digest.base64(digest.hmac_sha256("key", "The quick brown fox jumps over the lazy dog"));
		set resp.http.sha1_b64 = digest.base64(digest.hmac_sha1("key", "The quick brown fox jumps over the lazy dog"));
		set resp.http.md5_b64 = digest.base64(digest.hmac_md5("key", "The quick brown fox jumps over the lazy dog"));
		set resp.http.sha256_clean = digest.base64_decode(resp.http.sha256_b64);
		set resp.http.sha1_clean = digest.base64_decode(resp.http.sha1_b64);
		set resp.http.md5_clean = digest.base64_decode(resp.http.md5_b64);
		set resp.http.md5_b64url_nopad = digest.base64url_nopad("0x80070713463e7749b90c2dc24911e275");
		set resp.http.clean_nopad = digest.base64url_decode("MHg4MDA3MDcxMzQ2M2U3NzQ5YjkwYzJkYzI0OTExZTI3NQ");
		set resp.http.clean_nopad_hex = digest.base64url_decode_hex("MHg4MDA3MDcxMzQ2M2U3NzQ5YjkwYzJkYzI0OTExZTI3NQ");
		
		# Used to test +/ vs -_. The decoded variant is russian (I
		# think)
		set resp.http.ruski = digest.base64_decode("zprOsc67z47PgiDOv8+Bzq/Pg86xz4TOtQ==");
		set resp.http.ruski_b64 = "zprOsc67z47PgiDOv8+Bzq/Pg86xz4TOtQ==";
		set resp.http.ruski_url = digest.base64url_nopad(resp.http.ruski);

		set resp.http.hash_md5 = digest.hash_md5("foobar");
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.sha256 == "0xf7bc83f430538424b13298e6aa6fb143ef4d59a14946175997479dbc2d1a3cd8"
	expect resp.http.sha1 == "0xde7c9b85b8b78aa6bc8a7a36f70a90701c9db4d9"
	expect resp.http.md5 == "0x80070713463e7749b90c2dc24911e275"

	expect resp.http.sha256_b64 == "MHhmN2JjODNmNDMwNTM4NDI0YjEzMjk4ZTZhYTZmYjE0M2VmNGQ1OWExNDk0NjE3NTk5NzQ3OWRiYzJkMWEzY2Q4"
	expect resp.http.sha1_b64 == "MHhkZTdjOWI4NWI4Yjc4YWE2YmM4YTdhMzZmNzBhOTA3MDFjOWRiNGQ5"
	expect resp.http.md5_b64 == "MHg4MDA3MDcxMzQ2M2U3NzQ5YjkwYzJkYzI0OTExZTI3NQ=="
	
	expect resp.http.sha256_clean == "0xf7bc83f430538424b13298e6aa6fb143ef4d59a14946175997479dbc2d1a3cd8"
	expect resp.http.sha1_clean == "0xde7c9b85b8b78aa6bc8a7a36f70a90701c9db4d9"
	expect resp.http.md5_clean == "0x80070713463e7749b90c2dc24911e275"
	
	expect resp.http.clean_nopad == "0x80070713463e7749b90c2dc24911e275"
	expect resp.http.clean_nopad_hex == "307838303037303731333436336537373439623930633264633234393131653237350000"
	expect resp.http.md5_b64url_nopad == "MHg4MDA3MDcxMzQ2M2U3NzQ5YjkwYzJkYzI0OTExZTI3NQ"
	expect resp.http.ruski_url == "zprOsc67z47PgiDOv8-Bzq_Pg86xz4TOtQ"
       

	expect resp.http.hash_md5 == "3858f62230ac3c915f300c664312c63f"
}

client c1 -run
